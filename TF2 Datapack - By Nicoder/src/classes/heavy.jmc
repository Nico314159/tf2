new trim_pattern(heavy) {
    "asset_id": "tf2:heavy",
    "description": {
        "translate": "trim_pattern.tf2.heavy"
    },
    "template_item": "minecraft:iron_block"
}
generate_body_model(heavy);

class Class.Heavy {
    new predicates(is_revving) {
        "condition": "minecraft:all_of",
        "terms": [
            {
                "condition": "minecraft:entity_properties",
                "entity": "this",
                "predicate": {
                    "equipment": {
                        "mainhand": {
                            "items": [
                                "minecraft:crossbow"
                            ],
                            "predicates": {
                                "minecraft:custom_data": "{kind: \"real\"}"
                            }
                        }
                    }
                }
            },
            {
                "condition": "minecraft:entity_properties",
                "entity": "this",
                "predicate": {
                    "equipment": {
                        "offhand": {
                            "items": [
                                "minecraft:crossbow"
                            ],
                            "predicates": {
                                "minecraft:custom_data": "{kind: \"fake\"}"
                            }
                        }
                    }
                }
            }
        ]
    }
    new loot_tables(minigun_decoy) {
        "pools": [{
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:item",
                    "name": "minecraft:crossbow",
                    "functions": [
                        {
                            "function": "minecraft:set_name",
                            "name": {
                                "translate": "tf2.minigun",
                                "fallback": "Minigun (rev up with %s)",
                                "with": [{"keybind": "key.swapOffhand"}],
                                "color": "gray",
                                "italic": true
                            }
                        },
                        {
                            "function": "minecraft:set_custom_data",
                            "tag": "{kind: \"fake\"}"
                        },
                        {
                            "function": "minecraft:set_components",
                            "components": {
                                "minecraft:custom_model_data": 61000,
                                "minecraft:charged_projectiles": [{"id": "minecraft:arrow", "count": 1}],
                                "minecraft:unbreakable": {"show_in_tooltip": false}
                            }
                        }
                    ]
                }
            ]
        }]
    }
    new loot_tables(minigun_spinning) {
        "pools": [{
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:item",
                    "name": "minecraft:crossbow",
                    "functions": [
                        {
                            "function": "minecraft:set_name",
                            "name": {
                                "text": "Minigun",
                                "color": "gray",
                                "italic": true
                            }
                        },
                        {
                            "function": "minecraft:set_components",
                            "components": {
                                "minecraft:custom_model_data": 61001,
                                "minecraft:unbreakable": {"show_in_tooltip": false}
                            }
                        },
                        {
                            "function": "minecraft:set_custom_data",
                            "tag": "{
                                retina: {HorizontalCount: 1, VerticalCount: 1, CenteredCount: 0, EndpointEntity: true, TargetEntities: true, SpreadFactor: [210, 370], MaxRecursionDepth: 100},
                                attributes: {damage: {base: 9, maxRamp: 1.5}, attackDelay: 0.1d, spinupDelay: 0.87d, clip: 200, ammo: 0},
                                controls: {main_fire: true, alt_fire: false, reload: 0}, 
                                kind: \"real\"
                            }"
                        },
                        {
                            "function": "minecraft:reference",
                            "name": "tf2:load_crossbow",
                            "conditions": [
                                {
                                    "condition": "minecraft:entity_scores",
                                    "entity": "this",
                                    "scores": {
                                        "tf2.time_revved": {
                                            "min": {
                                                "type": "minecraft:score",
                                                "target": "this",
                                                "score": "tf2.rev_threshold",
                                                "scale": 1
                                            }
                                        }
                                    }
                                }
                            ]
                        }
                    ]
                }
            ]
        }]
    }
    new loot_tables(shotgun) {
        "pools": [{
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:loot_table",
                    "value": "tf2:crossbow_base",
                    "functions": [
                        {
                            "function": "minecraft:set_name",
                            "name": {
                                "text": "Shotgun",
                                "color": "gray",
                                "italic": true
                            }
                        },
                        {
                            "function": "minecraft:set_components",
                            "components": {
                                "minecraft:custom_model_data": 62000,
                                "minecraft:unbreakable": {"show_in_tooltip": false}
                            }
                        },
                        {
                            "function": "minecraft:set_custom_data",
                            "tag": "{
                                retina: {HorizontalCount: 3, VerticalCount: 3, CenteredCount: 1, EndpointEntity: true, TargetEntities: true, SpreadFactor: [100, 250], MaxRecursionDepth: 100},
                                attributes: {damage: {base: 6, maxRamp: 1.5}, attackDelay: 0.625d, reloadDelay: [0.87d, 0.51d], clip: 6, ammo: 32},
                                controls: {main_fire: true, alt_fire: false, reload: 1},
                                kind: \"real\"
                            }"
                        }
                    ]
                }
            ]
        }]
    }
    new loot_tables(right_fist) {
        "pools": [{
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:loot_table",
                    "value": "tf2:melee_base",
                    "functions": [
                        {
                            "function": "minecraft:set_name",
                            "name": {
                                "text": "Fists",
                                "color": "gray",
                                "italic": true
                            }
                        },
                        {
                            "function": "minecraft:set_components",
                            "components": {
                                "minecraft:custom_model_data": 63000,
                                "minecraft:unbreakable": {"show_in_tooltip": false}
                            }
                        },
                        {
                            "function": "minecraft:set_custom_data",
                            "tag": "{
                                retina: {HorizontalCount: 1, VerticalCount: 1, CenteredCount: 0, EndpointEntity: false, TargetEntities: true, SpreadFactor: [0, 0], MaxRecursionDepth: 5},
                                attributes: {damage: {base: 65}, attackDelay: 0.8d, range: 3.2d, isMelee: true},
                                controls: {main_fire: true, alt_fire: false, reload: 0}
                            }"
                        }
                    ]
                }
            ]
        }]
    }
    new loot_tables(left_fist) {
        "pools": [{
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:item",
                    "name": "minecraft:clay_ball",
                    "functions": [
                        {
                            "function": "minecraft:set_name",
                            "name": {"text": ""}
                        },
                        {
                            "function": "minecraft:set_components",
                            "components": {"minecraft:custom_model_data": 63000}
                        },
                        {
                            "function": "minecraft:set_custom_data",
                            "tag": "{ attributes: {isMelee: true} }"
                        }
                    ]
                }
            ]
        }]
    }

    function give() {
        clear @s;
        loot replace entity @s hotbar.0 loot tf2:class/heavy/minigun_decoy;
        loot replace entity @s weapon.offhand loot tf2:class/heavy/minigun_spinning;
        loot replace entity @s hotbar.1 loot tf2:class/heavy/shotgun;
        loot replace entity @s hotbar.2 loot tf2:class/heavy/right_fist;
        loot replace entity @s armor.legs loot tf2:class/heavy/_model;

        lookup::inventory = @s::Inventory;
        tf2.primary_maxClip:@s = tf2.primary_clip:@s = lookup::inventory[{Slot:-106b}].components.minecraft:custom_data.attributes.clip;
        tf2.primary_maxAmmo:@s = tf2.primary_ammo:@s = lookup::inventory[{Slot:-106b}].components.minecraft:custom_data.attributes.ammo;
        tf2.secondary_maxClip:@s = tf2.secondary_clip:@s = lookup::inventory[1].components.minecraft:custom_data.attributes.clip;
        tf2.secondary_maxAmmo:@s = tf2.secondary_ammo:@s = lookup::inventory[1].components.minecraft:custom_data.attributes.ammo;
        tf2.rev_threshold:@s = lookup::inventory[{Slot:-106b}].components.minecraft:custom_data.attributes.spinupDelay * 20000;
    }
    @lazy
    function tick() {
        Weapons.get_ammo();
        HUD.actionbar();
        Weapons.set_ammo();

        attribute @s[predicate=tf2:class/heavy/is_revving] minecraft:generic.movement_speed modifier add 736c6f77-7768-696c-6572-657676696e67 "slowwhilerevving" -0.476 add_multiplied_total;
        attribute @s[predicate=!tf2:class/heavy/is_revving] minecraft:generic.movement_speed modifier remove 736c6f77-7768-696c-6572-657676696e67;
        attribute @s[predicate=tf2:class/heavy/is_revving] minecraft:generic.jump_strength modifier add 00000000-0000-0000-0000-6e6f6a756d70 "no jump" -1 add_multiplied_total;
        attribute @s[predicate=!tf2:class/heavy/is_revving] minecraft:generic.jump_strength modifier remove 00000000-0000-0000-0000-6e6f6a756d70;
        tf2.time_revved:@s[predicate=tf2:class/heavy/is_revving] += 1000;
        
        if (tf2.current_slot:@s != 0 && tf2.time_revved:@s != 0) {
            tf2.attack_delay:@s += tf2.rev_threshold:@s;
            tf2.time_revved:@s = 0;
        }

        tf2.time_revved:@s[predicate=!tf2:class/heavy/is_revving] = 0;
        Class.Heavy.inventory_lock();
    }
    function inventory_lock() {
        loot replace entity @s hotbar.1 loot tf2:class/heavy/shotgun;
        $_count_ = clear @s crossbow[custom_model_data=62000] 0;
        if ($_count_ != 1) clear @s crossbow[custom_model_data=62000];
        if ($_count_ != 1) loot replace entity @s hotbar.1 loot tf2:class/heavy/shotgun;

        loot replace entity @s hotbar.2 loot tf2:class/heavy/right_fist;
        $_count_ = clear @s snowball[custom_model_data=63000] 0;
        if ($_count_ != 1) clear @s snowball[custom_model_data=63000];
        if ($_count_ != 1) loot replace entity @s hotbar.2 loot tf2:class/heavy/right_fist;

        loot replace entity @s armor.legs loot tf2:class/heavy/_model;
        $_count_ = clear @s iron_leggings 0;
        if ($_count_ != 1) clear @s iron_leggings;
        if ($_count_ != 1) loot replace entity @s armor.legs loot tf2:class/heavy/_model;

        loot replace entity @s armor.head loot tf2:head;
        $_count_ = clear @s paper 0;
        if ($_count_ != 1) clear @s paper;
        if ($_count_ != 1) loot replace entity @s armor.head loot tf2:head;

        if (tf2.current_slot:@s == 2) {
            loot replace entity @s weapon.offhand loot tf2:class/heavy/left_fist;
            $_count_ = clear @s clay_ball[custom_model_data=63000] 0;
            if ($_count_ != 1) clear @s clay_ball[custom_model_data=63000];
            if ($_count_ != 1) loot replace entity @s weapon.offhand loot tf2:class/heavy/left_fist;
        } else {
            clear @s clay_ball[custom_model_data=63000];
        }

        $_crossbow_id_ = @s::Inventory[-1].components."minecraft:custom_model_data" * 0.001;

        if (predicate tf2:class/heavy/is_revving && $_crossbow_id_ === 61) return run {
            loot replace entity @s hotbar.0 loot tf2:class/heavy/minigun_spinning;
            $_count_ = clear @s crossbow[custom_model_data=61001, custom_data={kind: "real"}] 0;
            if ($_count_ != 1) clear @s crossbow[custom_model_data=61001, custom_data={kind: "real"}];
            if ($_count_ != 1) loot replace entity @s hotbar.0 loot tf2:class/heavy/minigun_spinning;

            loot replace entity @s weapon.offhand loot tf2:class/heavy/minigun_decoy;
            $_count_ = clear @s crossbow[custom_model_data=61000, custom_data={kind: "fake"}] 0;
            if ($_count_ != 1) clear @s crossbow[custom_model_data=61000, custom_data={kind: "fake"}];
            if ($_count_ != 1) loot replace entity @s weapon.offhand loot tf2:class/heavy/minigun_decoy;
        }

        loot replace entity @s hotbar.0 loot tf2:class/heavy/minigun_decoy;
        $_count_ = clear @s crossbow[custom_model_data=61000, custom_data={kind: "fake"}] 0;
        if ($_count_ != 1) clear @s crossbow[custom_model_data=61000, custom_data={kind: "fake"}];
        if ($_count_ != 1) loot replace entity @s hotbar.0 loot tf2:class/heavy/minigun_decoy;

        if (tf2.current_slot:@s != 0) return run clear @s crossbow[custom_data={kind: "real"}];

        loot replace entity @s weapon.offhand loot tf2:class/heavy/minigun_spinning;
        $_count_ = clear @s crossbow[custom_model_data=61001, custom_data={kind: "real"}] 0;
        if ($_count_ != 1) clear @s crossbow[custom_model_data=61001, custom_data={kind: "real"}];
        if ($_count_ != 1) loot replace entity @s weapon.offhand loot tf2:class/heavy/minigun_spinning;
    }
}